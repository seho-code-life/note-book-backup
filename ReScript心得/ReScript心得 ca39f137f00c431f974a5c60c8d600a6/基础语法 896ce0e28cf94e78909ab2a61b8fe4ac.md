# 基础语法

## let绑定

在rescript中不存在const和var, let关键字就会被编译为var

### 基础用法

```jsx
let hello = "hello"
let world = "world"
let message = hello ++ world
```

### 块级作用域绑定

字符串拼接使用 ++ 相连接, 同样的, let绑定支持块作用域返回值, 最后一行会被隐式返回, 比如这样

```jsx
let area = {
    let width = 10;
    let height = 20;
    width * height
}
```

### 绑定覆盖

其次, 绑定是**不可变**的, 就像ts中的const一样, 它会在编译时提示你; 我们如果要修改一个变量如何操作, 这就是绑定覆盖, 我们使用的绑定是上方距离最近的绑定

```jsx
var result = 0;
result = calculate(result);
result = calculateSomeMore(result);
```

比如我们有这样的一个例子, 实际上result在第二行中计算的结果, 只是为了第三行的再次计算, 所以我们可以称之为**注释**, 我们还有更简单的方案:

```jsx
let result = 0
let result = calculate(result)
let result = calculateSomeMore(result)
```

我们可以同时声明一模一样的绑定, 实际上它被编译为

```jsx
var result = calculate(0);
var result$1 = calculateSomeMore(result);
```

你可以发现编译的js中, 其实并没有对result做修改, 如果开发者需要真正的修改, rescript中有其他的特性支持, 但是为什么rescript会这么做, 是因为js的语法设计上是允许用户随意修改变量的, 这对用户和开发者来说都是一个潜在的威胁, 在res开发中我们不需要担心因为变量修改造成的bug, 而且会给我们一个优秀的开发习惯.

### 私有let绑定

在res模块中, 所有的代码都是默认导出公开的, 这就意味着, 我们有时候在程序中想刻意隐藏某些值时. 需要提供一个独立的模块签名并且列出它的类型

```jsx
module A: {
  let b: int
} = {
  let a = 3
  let b = 4
}
```

我们可以使用相对高版本的特性, 私有化let简化这个问题

```jsx
module A = {
  %%private(let a = 3)
  let b = 4
}
```

---

## 类型

rescript中的类型是语言设计的一大亮点, 结合官方文档它有如下几个特点

1. 强类型 (类型int不会变为string, 即类型不会变更)
2. 静态类型 (和ts一样, 在编译后会擦除类型, 不会在运行时因为类型损耗性能)
3. 健全 (type sound, 即类型不会出错, 也就是说一个类型是固定的, 不会因为猜测类型, 导致系统变得危险, 这也是和ts一个大大的区别)
4. 类型推导 (不必写出类型, 在res中全部会自动推导)
5. 快速 (拥有快速的类型检查工具)

### 类型推导

```jsx
let score = 10
let add = (a, b) => a + b
```

在这一段代码中, score因为10的原因, 被推导为int, 而add函数的a和b参数因为a + b的算数运算符 + 推断a和b是int

### 类型标注

和ts一样, 我们同样可以给一个值标注类型, 但是与ts不同的是, res不会认为你标注的就是对的, 如果推断的类型和标注的类型不一致, 会在编译中报错

```jsx
// 标注参数
let a: int = 1
let add2 = (a: int, b: int) => a + b
// 标注一个函数返回值
let add3 = (a: int, b: int): int => a + b
```

如果我们将add2函数改为

```jsx
let add2 = (a: string, b: int) => a + b
```

```jsx
This has type: string
  Somewhere wanted: int
  
  You can convert string to int with Belt.Int.fromString.
```

编译器会提示你, 推断的类型和标注不一致, 提示我们要转换一下类型, 并且给我们了一个提示函数

### 类型别名

ts有的, res也会有, 我们可以对一个类型进行起名, 通常是我们需要封装一个类型才会需要

```jsx
type scoreType = int
let b : scoreType = 1
```

### 类型参数 / 泛型

类型参数可以帮助我们更简洁的书写类型, 尽管res有着强大的类型推导, 我们可以使用’a来标注一个参数, 也就等同于ts代码中的<T>

```jsx
type coordinates<'a> = ('a, 'a, 'a)
```

我们只需要在参数前面加入`‘`即可, 和ts一样, 我们可以接受许多类型参数, 并且可以随意组合, 在res标准库中, 用类型参数解决了很多代码问题, 比如array的泛型, 不可能要定义多个`arrayOfString`, `arrayOfInt`, 有了泛型只需要定义一个`array`, 再把类型传递进去即可

### 递归类型

ts的类型支持递归, res也同样支持, 可以在一个类型中自己引入自己

```jsx
type rec person = {
  name: string,
  friends: array<person>
}
```

有一种递归类型是相互递归, 比如a依赖b, b同时又依赖a, 我们可以使用and来表达互相递归

```jsx
type rec student = {taughtBy: teacher}
and teacher = {students: array<student>}
```

### 类型逃生

res提供了类型逃生, 但是不像ts那样简单, 我倒是希望类型逃生越难越好, 因为我们编写的绝大多数代码都是可控的, 尽管它再动态, 也不要滥用这个特性

```jsx
external convertToFloat : int => float = "%identity"
// external是res的一个概念, 在这里你可以理解为let
let age = 10
let gpa = 2.1 +. convertToFloat(age)
// 被声明过的convertToFloat处理之后, 就可以转换类型了
```

---

## 基础类型

res在js之上, 新增了几个类型, 以便解决js的各种类型陷阱

### 字符串

```jsx
let greeting = "Hello world!"
let multilineGreeting = "Hello
 world!"
```

不同于js, 要拼接2个字符串要使用++

```jsx
// 要连接2个字符串，可以使用 ++
let name = greeting ++ " My name is Jack."
```

res也提供了字符串模板, 和es6一样

```jsx
let name = "Joe"

let greeting = `Hello
World
👋
${name}
`
```

值得注意的是, 我们在使用字符串模板时, 如果要插入非字符串的值, 比如一个int, 那么需要把int转换为字符串, 我们也可以使用**j**来表达**隐式转换**

```jsx
let age = 10
let message = j`Today I am $age years old.`
```

### 字符

我们在上方使用双引号表达一个字符串, 在这里我们要使用单引号表达一个字符

```jsx
let firstLetterOfAlphabet = 'a'
```

它被编译为

```jsx
var firstLetterOfAlphabet = /* 'a' */97;
```

### 正则表达式

```jsx
let r = %re("/b/g")
```

### 布尔类型

和js一样, 有&&, ||, ≥=等运算符, 而且==, === 等和js一样

### 整数

res的整数类型int和js不同, 只支持32位整数, 必要时截断, 也支持* + -等运算符号, 我们如果要绑定大的数字建议换为浮点类型. 和js一样, 为了可读性, 可以在int中添加下划线用于分割: `1_000_000`

### 浮点数

比如0.5 + 0.6, 同整数一样, 可以使用下划线进行分割

### Unit

unit类型只有一个值为`()`, 编译为undefined, 是一个哑类型, 作为占位符, 大多数情况不会需要它

---

## 元组

res有着元组功能, 在js中不存在, 但是它编译为js的数组, 元组有几个概念

1. 不可变
2. 有序
3. 创建时固定大小
4. 可以包含不同的值

和ts的元组的大部分概念基本一致, 你可以指定它的类型, 也可以让res推导

```jsx
let tu: (int, float) = (1, 1.0)
```

会被编译为js

```jsx
var tu = [
  1,
  1.0
];
```

> 元组没有大小为1的, 如果有, 就使用值本身即可
> 

我们定义了一个元组之后, 可以去解构它

```jsx
let (_, y, _) = my3dCoordinates // now you've retrieved y
```

元组是无法修改的, 但是如果有相关需求的话, 可以基于一个元组值创建一个新的元组

```jsx
let coordinates1 = (10, 20, 30)
let (c1x, _, _) = coordinates1
let coordinates2 = (c1x + 50, 20, 30)
```

你也可以像rust一样, 直接返回一个元组

```jsx
let getCenterCoordinates = () => {
  let x = doSomeOperationsHere()
  let y = doSomeMoreOperationsHere()
  (x, y)
}
```

---

## Record记录

res的record和js的对象很像, 也不要和ts的Record类型进行比较

比如我们声明一个record类型

```jsx
type person = {
  age: int,
  name: string,
}
```

那么我们继续写一段js对象代码

```jsx
let me = {
  age: 5,
  name: "Big ReScript"
}
```

### 自动推断类型

你会惊奇的发现, 在res中它自动推导为person了, 因为res会检查你的字段是否和已知文件模块下的类型相符合, 如果符合, 那么开发者无需手动指定类型, 如果在ts中, 那么我们需要这么写

```jsx
let me: person = {}
```

### 访问属性

那么我们要访问一个record属性, 也是和js一样的

```jsx
let name = me.name
```

### 修改record对象

如果我们要更改对象, 是不允许的, 但是我们可以创建一个新的对象, 用`展开运算符…`

```jsx
let meNextYear = {...me, age: me.age + 1}
```

### 可修改的record属性

要更改某个属性的值是被允许的, 我们只需要在record前面加入`mutable`

```jsx
type person = {
  name: string,
  mutable age: int
}

let baby = {name: "Baby ReScript", age: 5}
me.age = 18
```

### 可选record属性

那么我们在开发中, 是需要灵活性的操作数据的, 我们不可能永远初始化整个对象, 所以我们需要有类似ts对象中的可选功能`?`

```jsx
type person = {
  age: int,
  name?: string
}
```

如果我们使用`?`标记了一个属性, 那么我们在使用时, 如果没传入name, 那么name将会是`None`类型

那么如果我们的record类型中存在可选, 那么如何通过`…` 修改对象呢? 首先我们可以不用关心它是否可选, 直接设置值即可, 和我们正常情况是一样的, 也符合我们js开发者的直观

```jsx
type person = {
  age: int,
  name?: string
}

let me = {
  age: 123,
  name: "Hello"
}

let withoutName = {
  ...me,
  name: "New Name"
}
```

当然你有时候并不想设置name这个可选字段一个具体的值, 那么你可以直接使用`Option`, 但是你需要在值前面加`?`

```jsx
let maybeName = Some("My Name")

let withoutName = {
  ...me,
  name: ?maybeName
}
```

或者你可以直接一鼓作气的设置为`?None`

```jsx
let withoutName = {
  ...me,
  name: ?None
}
```

### 可选record中的模式匹配

模式匹配是res中的一大特色, 在笔记之后也会详细介绍这一强大特性, 模式匹配简单来说就是抛弃掉杂乱无章的if else, 用简洁的语法可以做逻辑判断, 而在res中的模式匹配会生成出高质量的逻辑判断.

在这一部分中, 我们可能看不太懂模式匹配, 不过不用担心, 我们只需要get最核心的内容就好.

1. 首先我们如果对可选值直接进行模式匹配, 我们顺着思路写就可以, 没有什么需要注意的

```jsx
let isRescript = switch me.name {
| Some("ReScript") => true
| Some(_) | None => false
}
```

1. 但是我们如果对对象做模式匹配时, 类型系统会`“挑出可选值”`

```jsx
let isRescript = switch me {
| {name: "ReScript"} => true
| _ => false
}
```

1. 但是我们有时候就想知道某个可选值是否设置了咋办?

```jsx
let nameWasSet = switch me {
| {name: ?None} => false
| {name: ?Some(_)} => true
}
```

在None和Some之前只需要加一个?, 来告知模式匹配引擎: `“我要匹配一下可选值”`

### 关于record技巧

大家学了很久的res了, 也了解了res是根据上下文自动推断类型的, 而且我们也知道是根据距离当前代码上方最近的进行推断, 比如:

```jsx
type  = {age: int, name: string}
type monster = {age: int, hasTentacles: bool}

let getAge = (entity) => entity.age
```

getAge的函数中的entity参数会被推断为monster类型, 所以person类型的record值再传入getAge时, 是肯定会报错的, **尽管它存在.age的必选字段**

### record设计决策

这一个部分, 我将尽可能的比官方文档讲的更明白

1. 为什么在开发中record > 对象, 因为record是类型化的, 也是独一无二的, 很大程度避免了我们刚刚了解到的程序隐藏漏洞: **具有相同字段名的不同对象传递给同一个函数**, 我们要避免这种情况
2. 大多数情况下我们的数据形状是固定的;  如果不是, **变体**(res另外一个概念) + record可以解决这个问题
3. 我们学习的record类型是需要通过单一的显式声明来解决的, 在编译器方面会比对等类型 (元组)提示更好, 会更帮助开发者提前发现错误; 比如我们的数据模型发生了变更, 我只需要修改record类型字段即可, 这样编译器会自动提示我, 哪里用到了这个record, 会让重构更方便, 但是在对等类型时, 程序员是不知道是定义错了, 还是使用错了. (这个情况也同样适用于Ts开发者)

### 名词科普

- 名义类型: 指的是需要显式定义而且具有名字的类型
- 结构类型: 不是由名称或者其他因素产生的类型, 而是由实际结构, 比如典型的元组

```jsx
let tu: (int, float) = (1, 1.0)
```

---

## 变体

变体概念的存在可以允许我们定义**或者**, 我们可以通过`|`分割来表达一个类型

```jsx
type myResponse =
  | Yes
  | No
  | PrettyMuch

let areYouCrushingIt = No
```

此时areYouCrushingIt的值为0, 我们需要注意的是变体的规则是首字母大写, myResponse就是一个变体类型, 分别代表了3种情况

### 显式定义

我们需要手动定义一个变体, 并且可以引用它, 如果在其他文件模块中, 需要通过模块名访问到变体类型

```jsx
// Zoo.res
type animal = Dog | Cat | Bird
```

```jsx
// Example.res
let pet: Zoo.animal = Dog // preferred
// or
let pet2 = Zoo.Dog
```

### 构造器参数

我们在变体中也可以编写一段构造器, 可以让单纯的变体承载更多的数据

```jsx
type account =
  | None
  | Instagram(string)
  | Facebook(string, int)
```

```jsx
let myAccount = Facebook("Josh", 26)
let friendAccount = Instagram("Jenny")
```

它被编译为js

```jsx
var myAccount = {
  TAG: /* Facebook */1,
  _0: "Josh",
  _1: 26
};

var friendAccount = {
  TAG: /* Instagram */0,
  _0: "Jenny"
};
```

那么你可能觉得编译出来的js很丑, 那我们再来试试内联记录, 即record类型直接在变体中表达

```jsx
type user =
  | Number(int)
  | Id({name: string, password: string})

let me = Id({name: "Joe", password: "123"})
```

```jsx
var me = {
  TAG: /* Id */1,
  name: "Joe",
  password: "123"
};
```

我们可以清楚的看到内联record在变体中表现的很好, 输出的js也很优雅, 能承载更多信息, 同样的我们可以不使用内联record, 我们直接传递一个record进来看看

```
type u = {name: string, password: string}

type user =
  | Number(int)
  | Id(u)

let me = Id({name: "Joe", password: "123"})
```

```jsx
var me = {
  TAG: /* Id */1,
  _0: {
    name: "Joe",
    password: "123"
  }
};
```

由此可见, 我并不推荐大家这样写, 因为输出不够优雅, 而且性能也比较差

### 输出

我们回过头来, 再仔细检查一下变体的输出, 首先变体会有3种形态的js输出

1. 没有payload, 会被编译为一个数字
2. 带payload的构造器, 会被编译为一个对象, 附带一个Tag字段, 还有_01, _02之类的; 但是如果一个变体仅仅只包含一个带payload的构造器, 那么就没有Tag字段, 因为Tag字段是用来区分不同变体的
3. 使用record作为payload的变体, 输出一个优雅的js对象

### 技巧

1. 我们在ts中可以经常这样描述一个变体

```jsx
type myType = int | string
```

在res中不会被允许, 你必须给每个分支一个构造器, 这可以为你避免很多错误

```jsx
type myType = Int(int) | String(string)
```

1. 在record的章节中, 我们介绍了一个函数是不能接受属性相同的2个record类型, 变体也是一样的, 尽管构造器相同, 我们仍然不能把2个变体混为一谈, 在之后会有一个另外的概念叫做“**多态变体**”可以解决这个问题

### 决策

变体是至关重要的, 作为一个类型系统, 不能直接消除bug, 我们程序员产生bug的主要来源就是没有覆盖到边界情况, 所以我们在设计数据结构的时候, 需要我们正确建模, 比如一个数据需要是**这个**或者**那个**, 那么变体就尤为重要了, 再搭配上res的模式匹配, 我们可以完美覆盖所有的情况. 

或者从性能上讲, res中的变体是可以加快程序运行速度的, 比如这里有一段js代码

```jsx
let data = 'dog'
if (data === 'dog') {
  ...
} else if (data === 'cat') {
  ...
} else if (data === 'bird') {
  ...
}
```

它是一个O(n)的匹配, 如果我们用变体改写的话, 是这样子的

```jsx
type animal = Dog | Cat | Bird
let data = Dog
switch data {
| Dog => Js.log("Wof")
| Cat => Js.log("Meow")
| Bird => Js.log("Kashiiin")
}
```

res的编译器会自动把animal联想为0,1,2, 然后会直接编译switch, 然后进行一个skip

```jsx
console.log("Wof");

var data = /* Dog */0;
```

由此可见, res宣称可以生成更干净的js代码, 并不是空穴来风

---

## 多态变体

在上一章节中我们讲解了变体, 而多态变体类似于变体, 但是有一些区别

1. 声明方式不同, 以#开头, 构造器的名字不需要首字母大写
2. 不需要定义类型, 全部是推断出来的
3. 可以共享构造器, 随意组合

我们这样创建一个多态变体

```jsx
let myColor = #red
let myLabel = #"aria-hidden"
let myNumber = #7
```

可以很清晰的输出js

```jsx
var myColor = "red";
var myLabel = "aria-hidden";
var myNumber = 7;
```

所以它可以很好的和js互操作, 比如可以直接绑定字符串和对象, 对建模而言非常有用, 和ts一样, 但是唯一和ts不同的是, 这样的写法不会和普通字符串和数字搞混.

多态变体是结构化类型, 类型是可推导的, 但是你仍然可以事先声明一个类型

```jsx
type color = [#red | #green | #blue]
```

```jsx
let render = (myColor: [#red | #green | #blue]) => {
  switch myColor {
  | #blue => Js.log("Hello blue!")
  | #red
  | #green => Js.log("Hello other colors")
  }
}
```

### 构造器参数

```jsx
// 构造器参数, 和普通的变体是一样的
type account = [
  | #Anonymous
  | #Instagram(string)
  | #Facebook(string, int)
]

let me: account = #Instagram("Jenny")
let him: account = #Facebook("Josh", 26)
```

```jsx
// 同样的, 你可以用其他类型构造多态变体

type red = [#Ruby | #Redwood | #Rust]
type blue = [#Sapphire | #Neon | #Navy]
```

组合而成的多态变体, 模式匹配也可以用一下特殊的匹配方法

```jsx
// 那么对于这种混合的多态变体, 模式匹配得要使用稍微复杂一点的, 也就是类似于的展开运算符

switch myColor {
| #...blue => Js.log("This blue-ish")
| #...red => Js.log("This red-ish")
| other => Js.log2("Other color than red and blue: ", other)
}
```

```jsx
// 当然你可以全部展开进行模式匹配
switch myColor {
| #Sapphire | #Neon | #Navy => Js.log("This is blue-ish")
| #Ruby | #Redwood | #Rust => Js.log("This is red-ish")
| other => Js.log2("Other color than red and blue: ", other)
}
```

### 多态变体的类型约束

```jsx
let basic: [#Red] = #Red
let foreground: [> #Red] = #Green
let background: [< #Red | #Blue] = #Red
```

如果我们要编写一个开放的多态类型的话, 可以借助类型参数扩展已有多态编译

```jsx
type basicBlueTone<'a> = [> #Blue | #DeepBlue | #LightBlue ] as 'a
type color = basicBlueTone<[#Blue | #DeepBlue | #LightBlue | #Purple]>
```

### 强制类型转换

零成本从多态变体切换到string

```jsx
type company = [#Apple | #Facebook]
let theCompany: company = #Apple

let message = "Hello " ++ (theCompany :> string)
```

### 总结

我们学完多态变体之后, 我们会发现多态变体很自由, 可以优雅的和js互操作, 也有类型组合的能力, 模式匹配也很方便, 而且不用像变体这样的名义类型需要手动写一堆建模. 所以这一章我们就来讲讲变体类型有哪些坑

1. 编译器报错, 原因就是变体是名义类型的, 报错可以更好的溯源, 简而言之就是好排查错误
2. 重构多态变体也很难, 因为我们修改变体和修改js变量一样不受限制, 但是普通变体的修改需要显式的指定来源

```jsx
let myFruit = #Apple
let mySecondFruit = #Apple
let myCompany = #Apple
```

如上文例子, 我们修改第一个之后, 不意味着我们要修改第二个和第三个, 而普通变体会让你意识到哪里需要重构

1. 大家都知道res的编译器很强大, 经常会帮我们合并步骤并且简化, 但是如果我们写了多态变体, 其实编译器是无法做过多的优化的, 比如下面的代码, 尽管知道mycolor是red, 但是仍然无法自动删除模式匹配的blue-case

```jsx
let myColor = #red

switch myColor {
| #red => Js.log("Hello red!")
| #blue => Js.log("Hello blue!")
}
```

官方建议, 能用普通变体就用普通变体, 在需要和js互操作的情况下, 再使用多态变体

---

## Option, undefined, null

res中不存在null和undefind, 这就意味着我们的应用程序中不会存在undefined is not a function这种我们经常看到的报错啦! 但是res有一种变体类型来表达空这个存在, 设计参照了rust (我想大概是), 我们在之前的章节学习了普通变体, 那么option就是一个普通变体类型, 它的定义如下

```jsx
type option<'a> = None | Some('a)
```

意思就是这个类型要么是none, 要么就是被some包含的实际值.

我们可以用一个if表达式, 来对一个值进行绑定操作

```jsx
let licenseNumber =
  if personHasACar {
    Some(5)
  } else {
    None
  }
```

同样的, 你可以使用强大的模式匹配, 对这个值进行安全的访问.

但是option和js的互操作也是很方便的, 通常情况下, Some(5)会被直接编译为5, None会被编译为undefined, 在js中如果你知道一个值可能是一个undefined, 那你就标注一个option<string>, 同样也可以给js传递一个none或者some, 可以让js正确解释

### 警告

1. 不要使用嵌套的Some(Some(Some(5)))
2. 不要使用多态变体option, 要使用具体的变体类型, 比如我们上面举的例子, 通过if结构语句来绑定一个值
3. option类型不会对null或者undefined做区分, 在res中会有一些其他函数(标准库函数)帮助我们创建null/undefined, 更加便利的和js进行互操作

[Null, Undefined and Option | ReScript Language Manual](https://rescript-idea.github.io/docs-cn/manual/latest/null-undefined-option#%E8%AD%A6%E5%91%8A-2)

---

## 数组和列表

res中的数组和js中的数组是一样的, 它可以很好的和js进行互操作; 数组的类型必须一致

```jsx
let myArray = ["hello", "world", "how are you"]

let firstItem = myArray[0]

myArray[0] = "hey" // now ["hey", "world", "how are you"]

let pushedValue = Js.Array2.push(myArray, "bye")
```

但是在res中提供了一种单链表实现的列表功能(同数组一样, 内容必须类型相同), 这在js中是不存在的, 优点就是头插速度非常快

```jsx
let myList = list{1, 2, 3}
```

如果我们要进行一个头插, 就可以这么做

```jsx
let anotherList = list{0, ...myList}
```

它是无副作用的, 而且很高效, 因为它们是共享mylist的, 编译后的js如下:

```jsx
var anotherList = {
  hd: 0,
  tl: myList
};
```

但是遇到要插入多次的情况, 就不能像这样展开了

```jsx
list{a, ...b, ...c}
```

res并不支持, 因为在这里b要以此插入到b中, 这是一个O(b)的操作, 如果非要插入多个, 就要使用res提供的concat标准库api, 但是仍然不推荐.

### 总结

我们在使用数组时, 就像使用js一样, 可以扩展, 可以随意更改; 但是在使用列表时, 需要注意使用场景, 只适合头插, 并不建议插入多次, 和对某个元素进行修改(消耗很大)

---

## 函数

res的函数的写法和js大致相同, 并且也可以编译为纯净的js代码

```jsx
let greet = (name) => "Hello " ++ name
```

唯一不同的是, 我们的name参数不需要指定类型, 它是可以根据下文推导出来的, 在这里, 我们的返回值是字符串, 所以name也要求传递一个string类型

多个参数的表达是这样

```jsx
let add = (x, y, z) => x + y + z
add(1, 2, 3) // 6
```

有较长的函数体的时候, 可以用{}进行包裹, 和js函数一样

### 标签参数

res提供了一种参数假期, 即标签参数, 比如我们一个函数中很多参数的类型都是一样的, 那么在进行绑定参数时, 很难知道对应的参数是什么意义, 要非常注意原函数的参数顺序, 这会造成一定的开发负担, 所以提供了标签参数, 但是在js中, 我个人喜欢在函数中传入一个对象, 这样就不会存在参数顺序的问题了

```jsx
let addCoordinates = (~x, ~y) => {
  // use x and y here
}
// ...
addCoordinates(~x=5, ~y=6)
```

我们在声明函数时, 可以给参数加入标签, 这样在调用的时候可以显式的指定参数, 而不需要在意参数顺序; 同样的我们可以给参数起别名

```jsx
let drawCircle = (~radius as r, ~color as c) => {
  setColor(c)
  startAt(r, r)
  // ...
}
```

给参数主动标记类型, 这样推导的类型就会失效

```jsx
let drawCircle = (~radius as r: int, ~color as c: string) => {
  // code here
}
```

### 可选参数

res也和ts一样提供可选类型, 我们在调用函数时, 可以忽略它们

```jsx
let drawCircle = (~color, ~radius=?, ()) => {
  setColor(color)
  switch radius {
  | None => startAt(1, 1)
  | Some(r_) => startAt(r_, r_)
  }
}
```

我们可以通过`=?`这样的写法说明这个参数是一个可选参数, 此时radius的类型为Option, 是一个我们之前就学习过的变体类型, 它有可能是None, 也有可能是Some, 我们需要在代码块中做一个严谨的模式匹配.

> 注意: 在使用可选参数时, 必须保证它之后是一个非可选的参数, 这是由于res的设计原因导致的, 如果没有参数, 你可以使用unit类型, 比如一个()来占位 (unit类型在基础类型中我们也曾经说过)
> 

### 类型标注和函数签名

首先我们在可选参数中需要注意一点, 那就是函数的类型内外有别, 我们在给函数做类型签名的意义就是希望外界如何调用这个函数, 但是在函数参数内部, 我们其实希望函数内部如何使用这个参数.

```jsx
let drawCircle: (~color: color, ~radius: int=?, unit) => unit =
  (~color: color, ~radius: option<int>=?, ()) => {
    setColor(color)
    switch radius {
    | None => startAt(1, 1)
    | Some(r_) => startAt(r_, r_)
    }
  }
```

就像这个drawCircle函数我们希望对外提供一个可选的radius的int类型, 那么对于函数内部, radius的类型依然是int, 只不过是option<int>, 所以这个例子就说明了参数类型内外有别

### 直接传递option类型给参数

我们经常会遇到, 从一个地方拿到一个值, 就要传递给函数, 并且这个函数的参数有可能是可选的, 即None|Some, 有可能会这样写

```jsx
let result =
  switch payloadRadius {
  | None => drawCircle(~color, ())
  | Some(r) => drawCircle(~color, ~radius=r, ())
  }
```

我们需要做一个模式匹配, 分不同的情况进行参数传递

啊这? 太恶心了, 所以res提供了一个简写, 让我们直接可以把不确定的参数传递给函数可选参数

```jsx
let result = drawCircle(~color, ~radius=?payloadRadius, ())
```

### 可选默认值

给可选参数一个默认值, 此时这个参数不会是option类型

```jsx
let drawCircle = (~radius=1, ~color, ()) => {
  setColor(color)
  startAt(radius, radius)
}
```

## 递归

res默认的函数不支持递归, 我们需要在let后加入一个rec方可支持, 并且会将递归代码编译优化为高效的while循环, 比如这样

```jsx
let rec listHas = (list, item) =>
  switch list {
  | list{} => false
  | list{a, ...rest} => a === item || listHas(rest, item)
  }
```

编译为

```jsx
function listHas(_list, item) {
  while(true) {
    var list = _list;
    if (!list) {
      return false;
    }
    if (list.hd === item) {
      return true;
    }
    _list = list.tl;
    continue ;
  };
}
```

因为传统的递归会存在堆栈问题, 会造就先执行后运算, 而尾递归是先计算后执行, 在js引擎中就不需要切换堆栈帧了, 尾递归实现下, 是可以公用一个的

### 互递归函数

好吧, 我承认这个没啥用, 但是编译出来的js真优雅

```jsx
let rec callSecond = () => callFirst()
and callFirst = () => callSecond()
```

```jsx
function callSecond(_param) {
  while(true) {
    _param = undefined;
    continue ;
  };
}

function callFirst(_param) {
  while(true) {
    _param = undefined;
    continue ;
  };
}
```

### 异步

和async await一样, 和js的写法是完全一样的, 包括行为和ts都是一样的, 比如promise<string>被await之后会被推断为string

```jsx
let getUserName = async (userId) => userId

let greetUser = async (userId) => {
  let name = await getUserName(userId)
  "Hello " ++ name ++ "!"
}
```

错误处理, 我们经常会使用try-catch, 在res中和同步代码一样, 异步代码也可以使用这种方式来捕获异常, 它会被编译为一个try catch

```jsx
exception SomeReScriptException

let somethingThatMightThrow = async () => raise(SomeReScriptException)

let someAsyncFn = async () => {
  switch await somethingThatMightThrow() {
  | data => Some(data)
  | exception JsError(_) => None
  | exception SomeReScriptException => None
  }
}
```

js异常和rescript异常都可以被捕获到

### 忽略函数返回值ignore

```jsx
mySideEffect()->Promise.catch(handleError)->ignore

Js.Global.setTimeout(myFunc, 1000)->ignore
```

返回一个哑类型unit, 你可以简单粗浅的理解为函数返回值的any (ts概念)

### 接口文件

可以定义一个接口文件.resi, 为res实现文件标注类型

---

## 判断和循环

在res之前我们就使用过if判断, 它是一个表达式, 我们可以使用if进行一个求值操作

```jsx
let isMorning = true

let message = if isMorning {
  "Good morning!"
} else {
  "Hello!"
}
```

如果当前没有else分支, 会有一个() unit类型替代, 所以下面的2个写法是等价的

```jsx
if showMenu {
  displayMenu()
}
```

```jsx
if showMenu {
  displayMenu()
} else {
  ()
}
```

如果你不传递else分支, 又写了下面的代码, 会报一个类型错误, 原因就是else默认是unit类型, 而if分支则是int类型, 这对于我们的程序来说也是严谨的, 所以我们要习惯的准确提供else分支

```jsx
let result = if showMenu {
  1 + 2
}
```

当然还提供三目运算符, 但是我建议你使用if-else, 因为代码更加清晰可维护, 因为if-else编译之后也是三目.

res提供的for循环也是很强大的, 我们可以在for中指定一个起始和重点

```jsx
for x in 1 to 3 {
  Js.log(x)
}
```

编译为这样的js代码

```jsx
for(var x = 1; x <= 3; ++x){
  console.log(x);
}
```

我们可以将to关键字变为`downto`, 计数可以从大到小计数

res同样也提供一个while循环, 也可以绑定一个表达式

```jsx
while testCondition {
  // body here
}
```

在res中, 没有break和return可以跳出循环, 你需要使用res其他的概念, 比如可变绑定来完成这个功能, 如果你写过vue3相关的js库, 应该很容易理解下面这段代码, 可变绑定的概念会在之后继续深入

```jsx
let break = ref(false)

while !break.contents {
  if Js.Math.random() > 0.3 {
    break := true
  } else {
    Js.log("Still running")
  }
}
```

---

## 管道

接下来我为大家介绍一个res的特性, 好吧我承认这个特性对我来说并没有什么用, 我们设想一下这个场景

```jsx
validateAge(getAge(parseData(person)))
```

我们可以用一个`→`特性来表达, 让它成为一个链调用, 而且这个特性没有运行时开销, 因此我们可以用管道这样改写

```jsx
person
  ->parseData
  ->getAge
  ->validateAge
```

[Pipe | ReScript Language Manual](https://rescript-idea.github.io/docs-cn/manual/latest/pipe)

因为管道部分我不是很感冒, 而且实在是变换不过思维方式去这样调用函数, 我认为会对我造成一定的阅读负担, 所以管道部分如果读者们有兴趣, 就可以自行查阅文档

---

## 可变性

在之前的章节我们曾经谈到过一个概念, 就是在循环中我们没有类似`break`这样的关键字, 我们当时使用了一个res的概念叫做可变性, 通过可变性的变量被修改, 然后动态的阻止(跳出)循环

```jsx
let break = ref(false)

while !break.contents {
  if Js.Math.random() > 0.3 {
    break := true
  } else {
    Js.log("Still running")
  }
}
```

这是我们当时写的demo, 所以我们在这一章节快速过一下可变性概念.

let的绑定是不可变的, 这在我们最开始的教程已经说过了, 但是我们可以写以下的代码进行修改

```jsx
let myValue = ref(5)
```

它被编译为一个对象

```jsx
var myValue = {
  contents: 5
};
```

你可以这样去获得myValue

```jsx
let five = myValue.contents // 5
```

同样的, 你可以对contents进行重新绑定/赋值

```jsx
myValue.contents = 6
```

我们可以用这个语法糖来代替

```jsx
myValue := 6
```

> 提示, 你可以不使用可变性ref来修改let绑定, 你也可以覆盖let绑定来实现轻量级的修改
> 

```jsx
let result = 0
let result = calculate(result)
```

这样也是ok的, 所以想修改let绑定, 你可以自行选择2种方案

---

## ****惰性值****

这也是一个res中比较简单的概念, 所以我们用很简单的话去解释它, 就是一个值的绑定可以是惰性的

```jsx
let expensiveFilesRead = lazy({
  Js.log("Reading dir")
})
```

比方说在这个值中有一个昂贵的运算, 我们使用这个值的时候并不会立即计算, 我们需要显式的执行这个惰性值

```jsx
Lazy.force(expensiveFilesRead)
```

在第一次调用之后, 你无法再次触发执行惰性值, 所以请确保你使用惰性值的场景是一次性的, 比如从服务端获取的结果始终是一样的

---

## async & await Promise

res中的promise**最新的api**还不是标准库的一部分, 我们在这个示例中为了演示最新的promise api, 需要额外下载一个包

```jsx
npm install @ryyppy/rescript-promise --save
```

修改bsconfig.json配置文件

```jsx
{
  "bs-dependencies": ["@ryyppy/rescript-promise"]
}
```

编写以下函数

```jsx
let p1 = Promise.make((resolve, _reject) => {
  resolve(. "hello world")
})

let p2 = Promise.resolve("some value")
```

将编译为以下的js代码

```jsx
// Generated by ReScript, PLEASE EDIT WITH CARE
'use strict';

var p1 = new Promise((function (resolve, _reject) {
        resolve("hello world");
      }));

var p2 = Promise.resolve("some value");

exports.p1 = p1;
exports.p2 = p2;
/* p1 Not a pure module */
```

具体关于此库的使用方法, 可以前去文档观摩

[GitHub - ryyppy/rescript-promise: Proposal for a proper Js.Promise binding](https://github.com/ryyppy/rescript-promise)